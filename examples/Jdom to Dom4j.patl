( x : Element -> Element, y : String -> String ) {
    - x = new Element(y);
    + x = DocumentHelper.createElement(y);
}

( x : Element -> Element, y : String -> String, z : Namespace -> Namespace ) {
    - x = new Element(y, z);
    + x = DocumentHelper.createElement(new QName(y, z));
}

( x : Element -> Element, y : Element -> Element, z : String -> String )
{
    - y = x.addContent(z);
    + y = x.addText(z);
}

( x : Element -> Element, y : Element -> Element, z : Content -> Node )
{
    - y = x.addContent(z);
    + y = x.add(z);
}

( obj : Element -> Element, res : Element -> Element, content : Content -> Node, 
    pos : int -> int )
{
    - res = obj.addContent(pos, content);
    + List list = obj.content();
    + list.add(pos, content);
}

( obj : Element -> Element, res : List -> List )
{
    - res = obj.getContent();
    + res = obj.content();
}

( obj : Element -> Element, res : List -> List, 
    localName : String -> String, namespace : Namespace -> Namespace )
{
    - res = obj.getChildren();
    + res = obj.elements();
}

( obj : Element -> Element, res : List -> List, 
    localName : String -> String, namespace : Namespace -> Namespace )
{
    - res = obj.getChildren(localName);
    + res = obj.elements(localName);
}

( obj : Element -> Element, res : List -> List, 
    localName : String -> String, namespace : Namespace -> Namespace )
{
    - res = obj.getChildren(localName, namespace);
    + res = obj.elements(new QName(localName, namespace));
}

( obj : Element -> Element, res : List -> List, name : String -> String,
    ns : Namespace -> Namespace )
{
    - res = obj.getChild( name );
    + res = obj.element( name );
}

( obj : Element -> Element, res : List -> List, name : String -> String,
    ns : Namespace -> Namespace )
{
    - res = obj.getChild( name, ns );
    + res = obj.element( new QName(name, ns) );
}

( obj : Element -> Element, res : String -> String, 
    name : String -> String, def : String -> String, ns : Namespace -> Namespace )
{
    - res = obj.getAttributeValue(name);
    + res = obj.attributeValue(name);
}

( obj : Element -> Element, res : String -> String, 
    name : String -> String, def : String -> String, ns : Namespace -> Namespace )
{
    - res = obj.getAttributeValue(name, def);
    + res = obj.attributeValue(name, def);
}

( obj : Element -> Element, res : String -> String, 
    name : String -> String, def : String -> String, ns : Namespace -> Namespace )
{
    - res = obj.getAttributeValue(name, ns);
    + res = obj.attributeValue(new QName(name, ns));
}

( obj : Element -> Element, res : String -> String, 
    name : String -> String, def : String -> String, ns : Namespace -> Namespace )
{
    - res = obj.getAttributeValue(name, ns, def);
    + res = obj.attributeValue(new QName(name, ns), def);
}

( obj : Element -> Element, res : Attribute -> Attribute, name : String -> String,
    ns : Namespace -> Namespace )
{
    - res = obj.getAttribute( name );
    + res = obj.attribute( name );
}

( obj : Element -> Element, res : Attribute -> Attribute, name : String -> String,
    ns : Namespace -> Namespace )
{
    - res = obj.getAttribute( name, ns );
    + res = obj.attribute( new QName(name, ns) );
}

( obj : Element -> Element, res : List -> List )
{
    - res = obj.getAttributes();
    + res = obj.attributes();
}

( obj : Element -> Element, res : String -> String, name : String -> String,
    ns : Namespace -> Namespace )
{
    - res = obj.getChildText( name );
    + res = obj.elementText( name );
}

( obj : Element -> Element, res : String -> String, name : String -> String,
    ns : Namespace -> Namespace )
{
    - res = obj.getChildText( name, ns );
    + res = obj.elementText( new QName(name, ns) );
}

( obj : Element -> Element, res : String -> String, name : String -> String,
    ns : Namespace -> Namespace )
{
    - res = obj.getChildTextTrim( name );
    + res = obj.elementTextTrim( name );
}

( obj : Element -> Element, res : String -> String, name : String -> String,
    ns : Namespace -> Namespace )
{
    - res = obj.getChildTextTrim( name, ns );
    + res = obj.elementTextTrim( new QName(name, ns) );
}

( obj : Element ->  Element, res : Element -> Element )
{
    - res = obj.getParentElement();
    + res = obj.getParent();
}

( obj : Element -> Element, res : Namespace -> Namespace )
{
    - res = obj.getNamespace();
    + res = obj.getNamespace();
}

( obj : Element -> Element, res : String -> String )
{
    - res = obj.getNamespacePrefix();
    + res = obj.getNamespacePrefix();
}

( obj : Element -> Element, res : Namespace -> Namespace, pre : String -> String )
{
    - res = obj.getNamespace( pre );
    + res = obj.getNamespaceForPrefix( pre );
}

( obj : Element -> Element, text : String -> String )
{
    - obj.setText(text);
    + obj.setText(text);
}

( obj : Element -> Element, name : String -> String )
{
    - obj.setName(name);
    + obj.setName(name);
}

( obj : Element -> Element, ns : Namespace -> Namespace )
{
    - obj.setNamespace(ns);
    + obj.setQName(new QName(obj.getName(), ns));
}

( obj : Element -> Element, name : String ->  String, value : String -> String,
    attr : Attribute -> Attribute, ns : Namespace -> Namespace )
{
    - obj.setAttribute( name, value );
    + obj.addAttribute( name, value );
}

( obj : Element -> Element, name : String ->  String, value : String -> String,
    attr : Attribute -> Attribute, ns : Namespace -> Namespace )
{
    - obj.setAttribute( attr );
    + obj.addAttribute( attr.getName(), attr.getValue() );
}

( obj : Element -> Element, name : String ->  String, value : String -> String,
    attr : Attribute -> Attribute, ns : Namespace -> Namespace )
{
    - obj.setAttribute( name, value, ns );
    + obj.addAttribute( new QName(name, ns), value );
}

( obj : Element -> Element, res : String -> String )
{
    - res = obj.getValue();
    + res = obj.getStringValue();
}

( obj : Element -> Element, res : boolean -> Element, ns : Namespace -> Namespace )
{
    - res = obj.addNamespaceDeclaration(ns);
    + res = obj.addNamespace(ns.getPrefix(), ns.getURI());
}

( obj : Element -> Element, res : int -> int, para : Content -> Node )
{
    - res = obj.indexOf(para);
    + res = obj.indexOf(para);
}

( obj : Element -> Element, res : boolean -> boolean, content : Content -> Node )
{
    - res = obj.removeContent(content);
    + res = obj.remove(content);
} 

( obj : Element -> Element, res : Element -> Element )
{
    - res = obj.detach();
    + res = obj.detach();
}

( obj : Element -> Element, res : Element -> Element )
{
    - res = obj.clone();
    + res = obj.createCopy();
}

( obj : Element -> Element, res : List -> List )
{
    - res = obj.cloneContent();
    + Element e = obj.createCopy();
    + res = obj.content();
}

( res : Document -> Document )
{
    - res = new Document();
    + res = DocumentHelper.createDocument();
}

( obj : Document -> Document, res : Element -> Element )
{
    - res = obj.getRootElement();
    + res = obj.getRootElement();
}

( obj : Document -> Document, ele : Element -> Element )
{
    - obj.setRootElement(ele);
    + obj.setRootElement(ele);
}

( obj : Document -> Document, res : String -> String )
{
    - res = obj.getName();
    + res = obj.getName();
}

( obj : Document -> Document, res : String -> String )
{
    - res = obj.getText();
    + res = obj.getText();
}

( obj : Document -> Document, res : Document -> Document, content : Content -> Node)
{
    - res = obj.addContent(content);
    + res = obj.add(content);
}

( obj : Document -> Document, res : Document -> Document, content : Content -> Node, 
    pos : int -> int )
{
    - res = obj.addContent(pos, content);
    + List list = obj.content();
    + list.add(pos, content);
}

( res : Attribute -> Attribute,  name : String -> String, value : String -> String, 
    ns : Namespace -> Namespace )
{
    - res = new Attribute( name, value );
    + res = DocumentHelper.createAttribute( null, name, value );
}

( res : Attribute -> Attribute,  name : String -> String, value : String -> String, 
    ns : Namespace -> Namespace )
{
    - res = new Attribute( name, value, ns );
    + res = DocumentHelper.createAttribute( null, new QName(name, ns), value );
}

( res : SAXBuilder -> SAXReader )
{
    - res = new SAXBuilder();
    + res = new SAXReader();
}

( obj : SAXBuilder -> SAXReader, res : Document -> Document, rd : Reader -> Reader,
    f : File -> File )
{
    - res = obj.build( rd );
    + res = obj.read( rd );
}

( obj : SAXBuilder -> SAXReader, res : Document -> Document, rd : Reader -> Reader,
    f : File -> File )
{
    - res = obj.build( f );
    + res = obj.read( f );
}

( res : DOMBuilder-> DOMReader)
{
    - res = new DOMBuilder();
    + res = new DOMReader();
}

( obj : DOMBuilder -> DOMReader, res : Document -> Document, doc : Document -> Document )
{
    - res = obj.build( doc );
    + res = obj.read( doc );
}

( res : Format -> OutputFormat ) 
{
    - res = Format.getPrettyFormat();
    + res = OutputFormat.createPrettyFormat();
}

( res : Format -> OutputFormat ) 
{
    - res = Format.getCompactFormat();
    + res = OutputFormat.createCompactFormat();
}

( res : Format -> OutputFormat ) 
{
    - res = Format.getRawFormat();
    + res = new OutputFormat();
}

( obj : Format -> OutputFormat, cd : String -> String  ) 
{
    - obj.setEncoding( cd );
    + obj.setEncoding( cd );
}

( res : XMLOutputter -> XMLWriter )
{
    - res = new XMLOutputter();
    + res = new XMLWriter();
}

( res : XMLOutputter -> XMLWriter, f : Format -> OutputFormat )
{
    - res = new XMLOutputter( f );
    + res = new XMLWriter( f );
}

( obj : XMLOutputter -> XMLWriter, doc : Document -> Document, os : OutputStream -> OutputStream,
    wrt : Writer -> Writer )
{
    - obj.output( doc, os );
    + obj.setOutputStream(os);
    + obj.write( doc );
}

( obj : XMLOutputter -> XMLWriter, doc : Document -> Document, os : OutputStream -> OutputStream,
    wrt : Writer -> Writer )
{
    - obj.output( doc, wrt );
    + obj.setWriter( wrt );
    + obj.write( doc );
}

( obj : XMLOutputter -> XMLWriter, res : String -> String, f : Format -> OutputFormat,
    ele : Element -> Element )
{
    m f = Format.getRawFormat();
    m obj = new XMLOutputter(f);
    - res = obj.outputString(ele);
    + res = ele.asXML();
}

( obj : XMLOutputter -> XMLWriter, f2 : Format -> OutputFormat )
{
    - obj = new XMLOutputter();
    - obj.setFormat(f2);
    + obj = new XMLWriter(f2);
}

( obj : XMLOutputter -> XMLWriter, f1 : Format -> OutputFormat, f2 : Format -> OutputFormat )
{
    - obj = new XMLOutputter(f1);
    - obj.setFormat(f2);
    + obj = new XMLWriter(f2);
}

( obj : XMLOutputter -> XMLWriter, f : Format -> OutputFormat, ele : Element -> Element,
    res : String -> String )
{
    m f = Format.getRawFormat();
    m obj.setFormat(f);
    - res = obj.outputString(ele);
    + res = obj.asXML();
}

( obj : XMLOutputter -> XMLWriter, res : String -> String, ele : Element -> Element, x : Format -> Format )
{
    - x = Format.getPrettyFormat();
    - obj = new XMLOutputter(x);
    - res = obj.outputString(ele);
    + StringWriter sw = new StringWriter();
    + obj = new XMLWriter( sw, OutputFormat.createPrettyPrint());
    + obj.write(ele);
    + res = sw.toString();
}

( obj : XMLOutputter -> XMLWriter, res : String -> String, ele : Element -> Element, x : Format -> Format)
{
    - x = Format.getCompactFormat();
    - obj = new XMLOutputter(x);
    - res = obj.outputString(ele);
    + StringWriter sw = new StringWriter();
    + obj = new XMLWriter( sw, OutputFormat.createCompactFormat() );
    + obj.write(ele);
    + res = sw.toString();
}

( obj : XPath -> XPath, path : String -> String )
{
    - obj = XPath.newInstance( path );
    + obj = DocumentHelper.createXPath( path );
}

( obj : JDOMXPath -> DefaultXPath, path : String -> String )
{
    - obj = new JDOMXPath( path );
    + obj = new DefaultXPath( path );
}

( obj : XPath -> XPath, res : Object -> Object, content : Object -> Object, 
    path : String -> String )
{
    - res = obj.selectSingleNode( content );
    + res = obj.selectSingleNode( content );
}

( obj : XPath -> XPath, res : Object -> Object, content : Object -> Object, 
    path : String -> String )
{
    - res = obj.selectSingleNode( content, path );
    + res = obj.selectSingleNode( content, path );
}

( obj : XPath -> XPath, res : List -> List, content : Object -> Object,
    path : String -> String )
{
    - res = obj.selectNodes( content );
    + res = obj.selectNodes( content );
}

( obj : XPath -> XPath, res : List -> List, content : Object -> Object,
    path : String -> String )
{
    - res = obj.selectNodes( content, path );
    + res = obj.selectNodes( content, path );
}

( obj : Namespace -> Namespace, res : Namespace -> Namespace, pre : String -> String, 
    uri : String -> String )
{
    - res = obj.getNamespace( pre );
    + res = obj.getNamespace( pre );
}

( obj : Namespace -> Namespace, res : Namespace -> Namespace, pre : String -> String, 
    uri : String -> String )
{
    - res = obj.getNamespace( pre, uri );
    + res = obj.getNamespace( pre, uri );
}